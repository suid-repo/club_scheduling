CREATE PROCEDURE [dbo].[P_CheckQueued]
	@eventId INT = 0
AS
	DECLARE @eventList EventListTableType
	DECLARE @cursor INT = 0

	SET TRANSACTION ISOLATION LEVEL REPEATABLE READ

	WHILE (@cursor < 10)
	BEGIN
		SET @cursor = @cursor + 1
		BEGIN TRY
			BEGIN TRANSACTION
			-- CHOOSE THE EVENT(S) TO CHECK
			IF @eventId > 0
				INSERT INTO @eventList VALUES (@eventId)
			ELSE
				INSERT INTO @eventList
				SELECT E.Id
				FROM [Events] AS E
				WHERE E.StartDate >= GETDATE()

			-- DO SOME LOGIC NOW
			DECLARE MY_CURSOR CURSOR LOCAL STATIC READ_ONLY FORWARD_ONLY
			FOR
			SELECT EventId
			FROM @eventList

			OPEN MY_CURSOR
			FETCH NEXT FROM MY_CURSOR INTO @eventId
			WHILE @@FETCH_STATUS = 0
			BEGIN
				EXEC P_CheckUsers2Kick @eventId
				EXEC P_CheckUsers2Add @eventId
				FETCH NEXT FROM MY_CURSOR INTO @eventId
			END 
			CLOSE MY_CURSOR
			DEALLOCATE MY_CURSOR
			COMMIT
			BREAK
		END TRY
		BEGIN CATCH
			ROLLBACK TRANSACTION
			IF(ERROR_NUMBER() = 1205)
				CONTINUE
			ELSE
				THROW
		END CATCH
	END

RETURN 0